---
description: 'Дополнительные сведения о: BC42358: поскольку этот вызов не ожидается, выполнение текущего метода будет продолжаться до завершения вызова'
title: Так как этот вызов не ожидается, выполнение существующего метода продолжается до тех пор, пока вызов не будет завершен.
ms.date: 07/20/2015
f1_keywords:
- bc42358
- vbc42358
helpviewer_keywords:
- BC42358
ms.assetid: 43342515-c3c8-4155-9263-c302afabcbc2
ms.openlocfilehash: d8bc987e59bf99535484a8b75d6fa7f149cd8f7b
ms.sourcegitcommit: ddf7edb67715a5b9a45e3dd44536dabc153c1de0
ms.translationtype: MT
ms.contentlocale: ru-RU
ms.lasthandoff: 02/06/2021
ms.locfileid: "99796838"
---
# <a name="bc42358-because-this-call-is-not-awaited-execution-of-the-current-method-continues-before-the-call-is-completed"></a><span data-ttu-id="9364a-103">BC42358: поскольку этот вызов не ожидается, выполнение текущего метода продолжится до завершения вызова.</span><span class="sxs-lookup"><span data-stu-id="9364a-103">BC42358: Because this call is not awaited, execution of the current method continues before the call is completed</span></span>

<span data-ttu-id="9364a-104">Так как этот вызов не ожидается, выполнение существующего метода продолжается до тех пор, пока вызов не будет завершен.</span><span class="sxs-lookup"><span data-stu-id="9364a-104">Because this call is not awaited, execution of the current method continues before the call is completed.</span></span> <span data-ttu-id="9364a-105">Рассмотрите возможность применения оператора `Await` к результату вызова.</span><span class="sxs-lookup"><span data-stu-id="9364a-105">Consider applying the `Await` operator to the result of the call.</span></span>

<span data-ttu-id="9364a-106">Текущий метод вызывает асинхронный метод, который возвращает <xref:System.Threading.Tasks.Task> или, <xref:System.Threading.Tasks.Task%601> и не применяет оператор [await](../operators/await-operator.md) к результату.</span><span class="sxs-lookup"><span data-stu-id="9364a-106">The current method calls an async method that returns a <xref:System.Threading.Tasks.Task> or a <xref:System.Threading.Tasks.Task%601> and doesn't apply the [Await](../operators/await-operator.md) operator to the result.</span></span> <span data-ttu-id="9364a-107">Вызов асинхронного метода запускает асинхронную задачу.</span><span class="sxs-lookup"><span data-stu-id="9364a-107">The call to the async method starts an asynchronous task.</span></span> <span data-ttu-id="9364a-108">Однако поскольку оператор `Await` не применяется, программа продолжает выполнение, не ожидая завершения задачи.</span><span class="sxs-lookup"><span data-stu-id="9364a-108">However, because no `Await` operator is applied, the program continues without waiting for the task to complete.</span></span> <span data-ttu-id="9364a-109">В большинстве случаев такое поведение не ожидается.</span><span class="sxs-lookup"><span data-stu-id="9364a-109">In most cases, that behavior isn't expected.</span></span> <span data-ttu-id="9364a-110">Обычно другие аспекты вызывающего метода зависят от результатов вызова или, как минимум, вызывающий метод должен завершиться до возврата из метода, который содержит вызов.</span><span class="sxs-lookup"><span data-stu-id="9364a-110">Usually other aspects of the calling method depend on the results of the call or, minimally, the called method is expected to complete before you return from the method that contains the call.</span></span>

<span data-ttu-id="9364a-111">Также важной проблемой является обработка исключений, возникающих в вызываемом асинхронном методе.</span><span class="sxs-lookup"><span data-stu-id="9364a-111">An equally important issue is what happens with exceptions that are raised in the called async method.</span></span> <span data-ttu-id="9364a-112">Исключение, возникающее в методе, который возвращает <xref:System.Threading.Tasks.Task> или <xref:System.Threading.Tasks.Task%601>, хранится в возвращенной задаче.</span><span class="sxs-lookup"><span data-stu-id="9364a-112">An exception that's raised in a method that returns a <xref:System.Threading.Tasks.Task> or  <xref:System.Threading.Tasks.Task%601> is stored in the returned task.</span></span> <span data-ttu-id="9364a-113">Если не ожидать задачу или явно не проверять исключения, исключение будет потеряно.</span><span class="sxs-lookup"><span data-stu-id="9364a-113">If you don't await the task or explicitly check for exceptions, the exception is lost.</span></span> <span data-ttu-id="9364a-114">Если ожидать задачу, ее исключение будет создано повторно.</span><span class="sxs-lookup"><span data-stu-id="9364a-114">If you await the task, its exception is rethrown.</span></span>

<span data-ttu-id="9364a-115">Рекомендуется всегда ожидать вызов.</span><span class="sxs-lookup"><span data-stu-id="9364a-115">As a best practice, you should always await the call.</span></span>

<span data-ttu-id="9364a-116">По умолчанию данное сообщение является предупреждением.</span><span class="sxs-lookup"><span data-stu-id="9364a-116">By default, this message is a warning.</span></span> <span data-ttu-id="9364a-117">Дополнительные сведения о сокрытии предупреждений и обработке предупреждений как ошибок см. в разделе [Configuring Warnings in Visual Basic](/visualstudio/ide/configuring-warnings-in-visual-basic).</span><span class="sxs-lookup"><span data-stu-id="9364a-117">For more information about hiding warnings or treating warnings as errors, see [Configuring Warnings in Visual Basic](/visualstudio/ide/configuring-warnings-in-visual-basic).</span></span>

<span data-ttu-id="9364a-118">**Идентификатор ошибки:** BC42358</span><span class="sxs-lookup"><span data-stu-id="9364a-118">**Error ID:** BC42358</span></span>

## <a name="to-address-this-warning"></a><span data-ttu-id="9364a-119">Устранение предупреждения</span><span class="sxs-lookup"><span data-stu-id="9364a-119">To address this warning</span></span>

<span data-ttu-id="9364a-120">Рекомендуется отключать предупреждение, только если вы уверены, что не нужно ожидать завершения асинхронного вызова и что вызванный метод не создаст исключения.</span><span class="sxs-lookup"><span data-stu-id="9364a-120">You should consider suppressing the warning only if you're sure that you don't want to wait for the asynchronous call to complete and that the called method won't raise any exceptions.</span></span> <span data-ttu-id="9364a-121">В этом случае можно отключить предупреждение, присвоив переменной результат вызова задачи.</span><span class="sxs-lookup"><span data-stu-id="9364a-121">In that case, you can suppress the warning by assigning the task result of the call to a variable.</span></span>

<span data-ttu-id="9364a-122">В следующем примере показано, как вызвать предупреждение, как отключить его и как ожидать вызова:</span><span class="sxs-lookup"><span data-stu-id="9364a-122">The following example shows how to cause the warning, how to suppress it, and how to await the call:</span></span>

```vb
Async Function CallingMethodAsync() As Task

    ResultsTextBox.Text &= vbCrLf & "  Entering calling method."

    ' Variable delay is used to slow down the called method so that you
    ' can distinguish between awaiting and not awaiting in the program's output.
    ' You can adjust the value to produce the output that this topic shows
    ' after the code.
    Dim delay = 5000

    ' Call #1.
    ' Call an async method. Because you don't await it, its completion isn't
    ' coordinated with the current method, CallingMethodAsync.
    ' The following line causes the warning.
    CalledMethodAsync(delay)

    ' Call #2.
    ' To suppress the warning without awaiting, you can assign the
    ' returned task to a variable. The assignment doesn't change how
    ' the program runs. However, the recommended practice is always to
    ' await a call to an async method.
    ' Replace Call #1 with the following line.
    'Task delayTask = CalledMethodAsync(delay)

    ' Call #3
    ' To contrast with an awaited call, replace the unawaited call
    ' (Call #1 or Call #2) with the following awaited call. The best
    ' practice is to await the call.

    'Await CalledMethodAsync(delay)

    ' If the call to CalledMethodAsync isn't awaited, CallingMethodAsync
    ' continues to run and, in this example, finishes its work and returns
    ' to its caller.
    ResultsTextBox.Text &= vbCrLf & "  Returning from calling method."
End Function

Async Function CalledMethodAsync(howLong As Integer) As Task

    ResultsTextBox.Text &= vbCrLf & "    Entering called method, starting and awaiting Task.Delay."
    ' Slow the process down a little so you can distinguish between awaiting
    ' and not awaiting. Adjust the value for howLong if necessary.
    Await Task.Delay(howLong)
    ResultsTextBox.Text &= vbCrLf & "    Task.Delay is finished--returning from called method."
End Function
```

<span data-ttu-id="9364a-123">В этом примере при выборе Call #1 или Call #2, неожидаемый асинхронный метод (`CalledMethodAsync`) завершается после того, как завершатся вызвавший его метод (`CallingMethodAsync`) и метод, вызвавший этот метод (`StartButton_Click`).</span><span class="sxs-lookup"><span data-stu-id="9364a-123">In the example, if you choose Call #1 or Call #2, the unawaited async method (`CalledMethodAsync`) finishes after both its caller (`CallingMethodAsync`) and the caller's caller (`StartButton_Click`) are complete.</span></span> <span data-ttu-id="9364a-124">Последняя строка в следующем выводе показывает, когда завершается вызванный метод.</span><span class="sxs-lookup"><span data-stu-id="9364a-124">The last line in the following output shows you when the called method finishes.</span></span> <span data-ttu-id="9364a-125">Вход и выход из обработчика событий, который вызывает `CallingMethodAsync` , в полном примере помечены в выводе.</span><span class="sxs-lookup"><span data-stu-id="9364a-125">Entry to and exit from the event handler that calls `CallingMethodAsync` in the full example are marked in the output.</span></span>

```console
Entering the Click event handler.
  Entering calling method.
    Entering called method, starting and awaiting Task.Delay.
  Returning from calling method.
Exiting the Click event handler.
    Task.Delay is finished--returning from called method.
```

## <a name="example"></a><span data-ttu-id="9364a-126">Пример</span><span class="sxs-lookup"><span data-stu-id="9364a-126">Example</span></span>

<span data-ttu-id="9364a-127">Следующее приложение Windows Presentation Foundation (WPF) содержит методы из предыдущего примера.</span><span class="sxs-lookup"><span data-stu-id="9364a-127">The following Windows Presentation Foundation (WPF) application contains the methods from the previous example.</span></span> <span data-ttu-id="9364a-128">Для настройки приложения выполните следующие действия.</span><span class="sxs-lookup"><span data-stu-id="9364a-128">The following steps set up the application:</span></span>

1. <span data-ttu-id="9364a-129">Создайте приложение WPF с именем `AsyncWarning`.</span><span class="sxs-lookup"><span data-stu-id="9364a-129">Create a WPF application, and name it `AsyncWarning`.</span></span>

2. <span data-ttu-id="9364a-130">В редакторе кода Visual Studio перейдите на вкладку **MainWindow.xaml** .</span><span class="sxs-lookup"><span data-stu-id="9364a-130">In the Visual Studio Code Editor, choose the **MainWindow.xaml** tab.</span></span>

     <span data-ttu-id="9364a-131">Если вкладка не отображается, откройте контекстное меню для MainWindow.xaml в **Обозревателе решений** и выберите пункт **Просмотреть код**.</span><span class="sxs-lookup"><span data-stu-id="9364a-131">If the tab isn't visible, open the shortcut menu for MainWindow.xaml in **Solution Explorer**, and then choose **View Code**.</span></span>

3. <span data-ttu-id="9364a-132">Замените код в представлении **XAML** файла MainWindow. XAML следующим кодом:</span><span class="sxs-lookup"><span data-stu-id="9364a-132">Replace the code in the **XAML** view of MainWindow.xaml with the following code:</span></span>

    ```xaml
    <Window x:Class="MainWindow"
            xmlns="http://schemas.microsoft.com/winfx/2006/xaml/presentation"
            xmlns:x="http://schemas.microsoft.com/winfx/2006/xaml"
            Title="MainWindow" Height="350" Width="525">
        <Grid>
            <Button x:Name="StartButton" Content="Start" HorizontalAlignment="Left" Margin="214,28,0,0" VerticalAlignment="Top" Width="75" HorizontalContentAlignment="Center" FontWeight="Bold" FontFamily="Aharoni" Click="StartButton_Click" />
            <TextBox x:Name="ResultsTextBox" Margin="0,80,0,0" TextWrapping="Wrap" FontFamily="Lucida Console"/>
        </Grid>
    </Window>
    ```

     <span data-ttu-id="9364a-133">В представлении **Конструктор** файла MainWindow.xaml появится простое окно, содержащее кнопку и текстовое поле.</span><span class="sxs-lookup"><span data-stu-id="9364a-133">A simple window that contains a button and a text box appears in the **Design** view of MainWindow.xaml.</span></span>

     <span data-ttu-id="9364a-134">Дополнительные сведения о конструкторе XAML см. в разделе [Creating a UI by using XAML Designer](/visualstudio/xaml-tools/creating-a-ui-by-using-xaml-designer-in-visual-studio).</span><span class="sxs-lookup"><span data-stu-id="9364a-134">For more information about the XAML Designer, see [Creating a UI by using XAML Designer](/visualstudio/xaml-tools/creating-a-ui-by-using-xaml-designer-in-visual-studio).</span></span> <span data-ttu-id="9364a-135">Сведения о том, как построить собственный простой пользовательский интерфейс, см. в подразделах "Создание приложений WPF" и "Создание простого MainWindow WPF" в разделе [Пошаговое руководство. Получение доступа к Интернету с помощью модификатора Async и оператора Await](../../programming-guide/concepts/async/walkthrough-accessing-the-web-by-using-async-and-await.md).</span><span class="sxs-lookup"><span data-stu-id="9364a-135">For information about how to build your own simple UI, see the "To create a WPF application" and "To design a simple WPF MainWindow" sections of [Walkthrough: Accessing the Web by Using Async and Await](../../programming-guide/concepts/async/walkthrough-accessing-the-web-by-using-async-and-await.md).</span></span>

4. <span data-ttu-id="9364a-136">Замените код в MainWindow.xaml.vb на приведенный далее.</span><span class="sxs-lookup"><span data-stu-id="9364a-136">Replace the code in MainWindow.xaml.vb with the following code.</span></span>

    ```vb
    Class MainWindow

        Private Async Sub StartButton_Click(sender As Object, e As RoutedEventArgs)

            ResultsTextBox.Text &= vbCrLf & "Entering the Click event handler."
            Await CallingMethodAsync()
            ResultsTextBox.Text &= vbCrLf & "Exiting the Click event handler."
        End Sub

        Async Function CallingMethodAsync() As Task

            ResultsTextBox.Text &= vbCrLf & "  Entering calling method."

            ' Variable delay is used to slow down the called method so that you
            ' can distinguish between awaiting and not awaiting in the program's output.
            ' You can adjust the value to produce the output that this topic shows
            ' after the code.
            Dim delay = 5000

            ' Call #1.
            ' Call an async method. Because you don't await it, its completion isn't
            ' coordinated with the current method, CallingMethodAsync.
            ' The following line causes the warning.
            CalledMethodAsync(delay)

            ' Call #2.
            ' To suppress the warning without awaiting, you can assign the
            ' returned task to a variable. The assignment doesn't change how
            ' the program runs. However, the recommended practice is always to
            ' await a call to an async method.

            ' Replace Call #1 with the following line.
            'Task delayTask = CalledMethodAsync(delay)

            ' Call #3
            ' To contrast with an awaited call, replace the unawaited call
            ' (Call #1 or Call #2) with the following awaited call. The best
            ' practice is to await the call.

            'Await CalledMethodAsync(delay)

            ' If the call to CalledMethodAsync isn't awaited, CallingMethodAsync
            ' continues to run and, in this example, finishes its work and returns
            ' to its caller.
            ResultsTextBox.Text &= vbCrLf & "  Returning from calling method."
        End Function

        Async Function CalledMethodAsync(howLong As Integer) As Task

            ResultsTextBox.Text &= vbCrLf & "    Entering called method, starting and awaiting Task.Delay."
            ' Slow the process down a little so you can distinguish between awaiting
            ' and not awaiting. Adjust the value for howLong if necessary.
            Await Task.Delay(howLong)
            ResultsTextBox.Text &= vbCrLf & "    Task.Delay is finished--returning from called method."
        End Function

    End Class

    ' Output

    ' Entering the Click event handler.
    '   Entering calling method.
    '     Entering called method, starting and awaiting Task.Delay.
    '   Returning from calling method.
    ' Exiting the Click event handler.
    '     Task.Delay is finished--returning from called method.

    ' Output

    ' Entering the Click event handler.
    '   Entering calling method.
    '     Entering called method, starting and awaiting Task.Delay.
    '     Task.Delay is finished--returning from called method.
    '   Returning from calling method.
    ' Exiting the Click event handler.
    ```

5. <span data-ttu-id="9364a-137">Нажмите клавишу F5, чтобы запустить программу, а затем нажмите кнопку **Start** .</span><span class="sxs-lookup"><span data-stu-id="9364a-137">Choose the F5 key to run the program, and then choose the **Start** button.</span></span>

     <span data-ttu-id="9364a-138">Ожидаемый результат отобразится в конце кода.</span><span class="sxs-lookup"><span data-stu-id="9364a-138">The expected output appears at the end of the code.</span></span>

## <a name="see-also"></a><span data-ttu-id="9364a-139">См. также</span><span class="sxs-lookup"><span data-stu-id="9364a-139">See also</span></span>

- [<span data-ttu-id="9364a-140">Оператор Await</span><span class="sxs-lookup"><span data-stu-id="9364a-140">Await Operator</span></span>](../operators/await-operator.md)
- [<span data-ttu-id="9364a-141">Асинхронное программирование с использованием Async и await</span><span class="sxs-lookup"><span data-stu-id="9364a-141">Asynchronous Programming with Async and Await</span></span>](../../programming-guide/concepts/async/index.md)
